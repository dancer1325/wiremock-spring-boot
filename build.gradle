buildscript {
	repositories {
		gradlePluginPortal()
		mavenCentral()
		mavenLocal()
	}
	dependencies {
		classpath "se.bjurr.gradle.java-convention:se.bjurr.gradle.java-convention.gradle.plugin:0.+"
	}
}

plugins {
	id 'org.wiremock.tools.gradle.wiremock-extension-convention' version '0.3.0'
}

apply plugin: "project-report"

group 'org.wiremock.integrations'

allprojects {
	apply plugin: "se.bjurr.gradle.java-convention"

	dependencies {
		testImplementation "org.springframework.boot:spring-boot-starter-test:3.3.4"
		testImplementation 'org.assertj:assertj-core:3.26.3'
		testImplementation platform('org.junit:junit-bom:5.11.2')
		testImplementation 'org.junit.jupiter:junit-jupiter'
		testImplementation 'org.junit.platform:junit-platform-launcher'

		constraints {
			implementation('org.apache.commons:commons-compress:1.26.0') {
				because 'version 1.24.0 has a vulnerability'
			}
		}
	}
}

dependencies {
	api 'org.wiremock:wiremock-standalone:3.9.1'
	api "org.springframework.boot:spring-boot-test:3.3.4"
	api "org.springframework:spring-test:6.1.13"
	api "org.slf4j:slf4j-api:2.0.16"
	api 'org.junit.jupiter:junit-jupiter-api:5.11.2'
}

shadowJar {
	mergeServiceFiles()

	exclude 'META-INF/maven/**'
	exclude 'META-INF/versions/17/**'
	exclude 'META-INF/versions/21/**'
	exclude 'module-info.class'
}

project('wiremock-spring-boot-example', {
	dependencies {
		implementation "org.springframework.boot:spring-boot-starter-web:3.3.4"
		implementation "org.springframework.boot:spring-boot-starter-webflux:3.3.4"

		testImplementation rootProject
		testImplementation 'io.rest-assured:rest-assured:5.5.0'
	}
})
